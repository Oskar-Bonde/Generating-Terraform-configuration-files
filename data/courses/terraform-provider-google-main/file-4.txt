# raw-data/terraform-provider-google-main\examples\example-versioned-module\ip
resource "google_compute_address" "ip_address" {
  # We'll only generate this block if the value of
  # has_labels is 0! Effectively an if statement.
  count = 1 - local.has_labels

  name = var.name
}

resource "google_compute_address" "ip_address_beta" {
  # And this block is only present if we have
  # at least one entry, effectively an elif.
  count = local.has_labels

  name   = var.name
  labels = var.labels
}


output "address" {
  description = "The generated address of the ip address"

  value = coalesce(
    element(concat(google_compute_address.ip_address.*.address, [""]), 0),
    element(
      concat(google_compute_address.ip_address_beta.*.address, [""]),
      0,
    ),
  )
}


variable "name" {
  description = "A name for the ip address resource"
}

variable "labels" {
  type        = map(string)
  description = "A map of key:value labels to apply to the ip address resource"
  default     = {}
}

locals {
  # This ends up being a boolean
  # 1 if there are any entries
  # 0 otherwise
  has_labels = min(1, length(var.labels))
}


