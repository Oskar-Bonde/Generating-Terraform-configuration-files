# raw-data/collabnix-course\beginners\azure\multiple_resources
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
#*          Create multiple resources using for_each   *#
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#

#
# - Provider Block
#

provider "azurerm" {
    version         =   ">=2.6"
    client_id       =   var.client_id
    client_secret   =   var.client_secret
    subscription_id =   var.subscription_id
    tenant_id       =   var.tenant_id
    
    features {}
}

#
# - Create multiple Resource Groups
#

resource "azurerm_resource_group" "rg" {
    for_each              =   var.resource_group
    name                  =   each.key
    location              =   each.value
    tags                  =   var.tags
}

#
# - Create a Virtual Network
#


resource "azurerm_virtual_network" "vnet" {
  resource_group_name   =   azurerm_resource_group.rg["Dev-RG"].name
  name                  =   var.virtual_network["name"]
  location              =   azurerm_resource_group.rg["Dev-RG"].location
  address_space         =   [var.virtual_network["address_range"]]
  tags                  =   var.tags
}


#
# - Create multiple Subnets inside the virtual network
#

resource "azurerm_subnet" "sn" {
   for_each             =   var.subnet
   name                 =   each.key
   resource_group_name  =   azurerm_resource_group.rg["Dev-RG"].name
   virtual_network_name =   azurerm_virtual_network.vnet.name
   address_prefixes     =   [each.value]
}

#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
#*     Create multiple resources using for_each        *#
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#

output "resource-group" {
    description =   "Map resource group name with location"
    value       =   { for i in azurerm_resource_group.rg: i.name => i.location }
}

output "vnet" {
    description =   "Print Vnet name and Address Space"
    value       =    {
        "Vnet_Name"     =   azurerm_virtual_network.vnet.name 
        "Vnet_Address"  =   azurerm_virtual_network.vnet.address_space  
    }
}

output "subnet" {
    description =   "Map subnet name with address_prefixes"
    value       =   { for s in azurerm_subnet.sn: s.name => s.address_prefixes }
}


#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
#*    Create multiple resources using for_each         *#
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#


# Service Principal Variables

variable "client_id" {
    description =   "Client ID (APP ID) of the application"
    type        =   string
}

variable "client_secret" {
    description =   "Client Secret (Password) of the application"
    type        =   string
}

variable "subscription_id" {
    description =   "Subscription ID"
    type        =   string
}

variable "tenant_id" {
    description =   "Tenant ID"
    type        =   string
}

variable "tags" {
    description     =   "Tags"
    type            =   map(string)
    default         =   {
        "Project"       =   "Collabnix"
        "Deployed_with" =   "Terraform"
        "Track"         =   "Beginner"
    }
}

#
# - Resource Group Variables
#

variable "resource_group" {
    description     =       "Create multiple resource groups"
    type            =       map(string)
    default         =       {
        "Dev-RG"        =       "South India"
        "QA-RG"         =       "West India"
        "Prod-RG"       =       "Central India"
    }
}

#
# - Virtual Network Variables
#

variable "virtual_network" {
    description     =       "Virtual Network variables"
    type            =       map(string)
    default         =       {
        "name"              =       "Dev-Vnet"
        "address_range"     =       "10.0.0.0/16"
    }
}

#
# - Subnet Variables
#

variable "subnet" {
    description     =       "Create multiple subnets"
    type            =       map(string)
    default         =       {
        "Web-Subnet"    =       "10.0.1.0/24"
        "App-Subnet"    =       "10.0.2.0/24"
        "DB-Subnet"     =       "10.0.3.0/24"
    }
}

