# raw-data/hashicorp-certified-terraform\07-Terraform-State\07-01-Terraform-Remote-State-Storage-and-Locking\terraform-manifests
# Terraform Block with AWS. Create backend as S3 for remote state storage
terraform {
  required_version = "~> 0.14" # which means any version equal & above 0.14 like 0.15, 0.16 etc and < 1.xx
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 3.0"
    }
  }
  backend "s3" {
    bucket = "terraform-stacksimplify"
    key    = "dev/terraform.tfstate"
    region = "us-east-1"   
  }
}

# Provider AWS block with region var.aws_region
provider "aws" {
  region  = var.aws_region
  profile = "default"
}

# Create EC2 Instance - Amazon Linux
resource "aws_instance" "my-ec2-vm" {
  ami           = data.aws_ami.amzlinux.id 
  instance_type = var.instance_type
  key_name      = "terraform-key"
	user_data = file("apache-install.sh")  
  vpc_security_group_ids = [aws_security_group.vpc-ssh.id, aws_security_group.vpc-web.id]
  tags = {
    "Name" = "amz-linux-vm"
  }
}

# output block, get EC2 Instance Public IP
output "ec2_instance_publicip" {
  value = aws_instance.my-ec2-vm.public_ip
}

# Output block, create public DNS URL 
output "ec2_publicdns" {
  value = aws_instance.my-ec2-vm.public_dns
}

# Get latest AMI ID for Amazon Linux2 OS
data "aws_ami" "amzlinux" {
  most_recent = true
  owners = [ "amazon" ]
  filter {
    name = "name"
    values = [ "amzn2-ami-hvm-*-gp2" ]
  }
  filter {
    name = "root-device-type"
    values = [ "ebs" ]
  }
  filter {
    name = "virtualization-type"
    values = [ "hvm" ]
  }
  filter {
    name = "architecture"
    values = [ "x86_64" ]
  }
}

