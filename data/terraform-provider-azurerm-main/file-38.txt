# raw-data/terraform-provider-azurerm-main\examples\databricks\customer-managed-key\managed-services
provider "azurerm" {
  features {}
}

data "azurerm_client_config" "current" {}

resource "azurerm_resource_group" "example" {
  name     = "${var.prefix}-databricks-managed-services"
  location = "West Europe"
}

resource "azurerm_databricks_workspace" "example" {
  depends_on = [azurerm_key_vault_access_policy.managed]

  name                        = "${var.prefix}-DBW"
  resource_group_name         = azurerm_resource_group.example.name
  location                    = azurerm_resource_group.example.location
  sku                         = "premium"
  managed_resource_group_name = "${var.prefix}-DBW-managed-services"

  managed_services_cmk_key_vault_key_id = azurerm_key_vault_key.example.id

  tags = {
    Environment = "Production"
  }
}

resource "azurerm_key_vault" "example" {
  name                = "${var.prefix}-keyvault"
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name
  tenant_id           = data.azurerm_client_config.current.tenant_id
  sku_name            = "premium"

  soft_delete_retention_days = 7
}

resource "azurerm_key_vault_key" "example" {
  depends_on = [azurerm_key_vault_access_policy.terraform]

  name         = "${var.prefix}-certificate"
  key_vault_id = azurerm_key_vault.example.id
  key_type     = "RSA"
  key_size     = 2048

  key_opts = [
    "decrypt",
    "encrypt",
    "sign",
    "unwrapKey",
    "verify",
    "wrapKey",
  ]
}

resource "azurerm_key_vault_access_policy" "terraform" {
  key_vault_id = azurerm_key_vault.example.id
  tenant_id    = azurerm_key_vault.example.tenant_id
  object_id    = data.azurerm_client_config.current.object_id

  key_permissions = [
    "get",
    "list",
    "create",
    "decrypt",
    "encrypt",
    "sign",
    "unwrapKey",
    "verify",
    "wrapKey",
    "delete",
    "restore",
    "recover",
    "update",
    "purge",
  ]
}

resource "azurerm_key_vault_access_policy" "managed" {
  key_vault_id   = azurerm_key_vault.example.id
  tenant_id      = azurerm_key_vault.example.tenant_id
  object_id      = "See the README.md file for instructions on how to lookup the correct value to enter here"

  key_permissions = [
    "get",
    "unwrapKey",
    "wrapKey",
  ]
}


variable "prefix" {
  description = "The Prefix used for all resources in this example"
}


